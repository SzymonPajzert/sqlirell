--- based on https://ci.apache.org/projects/asterixdb/sqlpp/manual.html
-- TODO make all expressions finish with Expr
-- TODO make all keywords case insensitive (probably)

terminator Query ";" ;
Prog. Program ::= [Query] ;

token Identifier (letter (letter | digit | '_' | '$' )*) ;
token DelimIdent '`' (char - '`')* '`'  ;

token DoubleString '"' ((char - '"') | ('\\' '"') )* '"'    ;
token SingleString '\'' ((char - '\'') | ('\\' '\'') )* '\'' ;

token FloatNum (digit)* ('.' digit (digit)* )? ( 'f' | 'F' ) ;
token DoubleNum (digit)* '.' digit (digit)* ;

ExpQuer. Query ::= Expression ;

SelQuer. Query ::= SelectStatement ;

separator Expression ","                                        ;
OperExpr. Expression ::= OpExpr                                 ;
CaseExpr. Expression ::= CaseExpr                               ;
QuanExpr. Expression ::= QuantExpr                              ;

-- operator expression
PathOpExpr.   OpExpr14 ::= PathExpr ;
ExistsOpExpr. OpExpr13 ::= "exists" OpExpr14 ;
ExponeOpExpr. OpExpr12 ::= OpExpr13 "^" OpExpr12 ; -- TODO check associativity
MultipOpExpr. OpExpr11 ::= OpExpr11 "*" OpExpr12 ;
DivisiOpExpr. OpExpr11 ::= OpExpr11 "/" OpExpr12 ;
ModuloOpExpr. OpExpr11 ::= OpExpr11 "%" OpExpr12 ;
AdditiOpExpr. OpExpr10 ::= OpExpr10 "+" OpExpr11 ;
SubstrOpExpr. OpExpr10 ::= OpExpr10 "-" OpExpr11 ;
NegationExpr. OpExpr9 ::= "-" OpExpr11 ;
PositiveExpr. OpExpr9 ::= "+" OpExpr11 ;

YIs. Is ::= "is" ;
NIs. Is ::= "is" "not" ;

BtwnAfterNot. AfterNot ::= "between" OpExpr6 "and" OpExpr6 ;
LikeAfterNot. AfterNot ::= "like" OpExpr4 ;
IsInAfterNot. AfterNot ::= "in" OpExpr4 ; 

ConcatOpExpr. OpExpr9 ::= OpExpr9 "||" OpExpr10 ;
IsNullOpExpr. OpExpr7 ::= OpExpr8 Is "null"    ;
IsMissOpExpr. OpExpr7 ::= OpExpr8 Is "missing" ;
IsUnknOpExpr. OpExpr6 ::= OpExpr8 Is "unknown" ;
IsBtwnOpExpr. OpExpr5 ::= OpExpr6 "between" OpExpr6 "and" OpExpr6 ;
NoAfteOpExpr. OpExpr5 ::= OpExpr6 "not" AfterNot;

IsEquaOpExpr. OpExpr4 ::= OpExpr5 "=" OpExpr5 ;
NoEquaOpExpr. OpExpr4 ::= OpExpr5 "~=" OpExpr5 ;
NoEquaOpExpr. OpExpr4 ::= OpExpr5 "!=" OpExpr5 ;
NoEquaOpExpr. OpExpr4 ::= OpExpr5 "<>" OpExpr5 ;
GraterOpExpr. OpExpr4 ::= OpExpr5 ">" OpExpr5 ;
GrOrEqOpExpr. OpExpr4 ::= OpExpr5 ">=" OpExpr5 ;
SmalerOpExpr. OpExpr4 ::= OpExpr5 "<" OpExpr5 ;
SmOrEqOpExpr. OpExpr4 ::= OpExpr5 "<=" OpExpr5 ;
IsLikeOpExpr. OpExpr3 ::= OpExpr4 "like" OpExpr4 ;
IsIn__OpExpr. OpExpr3 ::= OpExpr4 "in" OpExpr4 ;

NegatiOpExp. OpExpr2 ::= "not" OpExpr3 ;
ConiunOpExp. OpExpr1 ::= OpExpr1 "and" OpExpr2 ;
AlternOpExp. OpExpr ::= OpExpr "or" OpExpr1 ;

coercions OpExpr 14 ;

PrimaPathExp. PathExpr ::= PrimaryExpr ;
FieldPathExp. PathExpr ::= PathExpr "." VariableRef ;
INullPathExp. PathExpr ::= PathExpr "[" "?" "]" ;
IExprPathExp. PathExpr ::= PathExpr "[" Expression "]" ;

{-                       CASE EXPRESSIONS                        -}

SimpleCaseExpr. CaseExpr ::= "case" Expression CaseExprTail ;
SearchCaseExpr. CaseExpr ::= "case" CaseExprTail ;

NoElseCaseTail. CaseExprTail ::= [CaseExprBind] "end" ;
IsElseCaseTail. CaseExprTail ::= [CaseExprBind] "else" Expression "end" ;

separator nonempty CaseExprBind "" ;
CaseExprBind. CaseExprBind ::= "when" Expression "then" Expression ;


{-                    Quantified Expressions                     -}
IsEndQuantExpr. QuantExpr ::= QuantExprHelp "end";
NoEndQuentExpr. QuaneExpr ::= QuantExprHelp ;

-- IsMEnd. MEnd ::= "end" ;
-- TODO NoMEnd. MEnd ::= ;

separator nonempty QuantVariable "," ;
QuantExprHelp. QuantExprHelp ::= ExprQualifier [QuantVariable] "satisfies" Expression ;

QuantVariable. QuantVariable ::= VariableRef "in" Expression ;

SomeExprQual.  ExprQualifier ::= "some" ;
SomeExprQual.  ExprQualifier ::= "anu" ; -- TODO check if ANY and SOME are the same
EveryExprQuar. ExprQualifier ::= "every" ;



{-                     Primary Expressions                       -}
LiteralPrimaryExpr. PrimaryExpr1  ::= Literal                      ;
VariablPrimaryExpr. PrimaryExpr1  ::= VariableRef                  ;
SubquerPrimaryExpr. PrimaryExpr2  ::= SelectStatement1             ;
FunCallPrimaryExpr. PrimaryExpr   ::= PathExpr "(" [ Expression ] ")" ;
ConstruPrimaryExpr. PrimaryExpr1  ::= Constructor                  ;

_. PrimaryExpr  ::= PrimaryExpr1;
_. PrimaryExpr1 ::= PrimaryExpr2;

{-                     .  Literal                                -}
DStringLiteral.  Literal ::= DoubleString ;
SStringLiteral.  Literal ::= SingleString ;
IntegeLiteral.  Literal ::= Integer ;
FloatiLiteral.  Literal ::= FloatNum ;
DoubleLiteral.  Literal ::= DoubleNum ;
NullLiteral.    Literal ::= "null" ;
MissingLiteral. Literal ::= "missing" ;
TrueLiteral.    Literal ::= "true" ;
FalseLiteral.   Literal ::= "false" ;

-- TODO add escaping options
DoubleQuoteString. StringLiteral ::= "\"" Identifier "\"" ;
SingleQuoteString. StringLiteral ::= "\'" Identifier "\'" ;

IntegerLiteral. IntegerLiteral ::= Integer ;

-- TODO: FloatLiteral DoubleLiteral

{-                     .  Constructor                            -}
ArrayConstr. Constructor       ::= "[" [ Expression ] "]"		  ;
MultisetConst. Constructor     ::= "{{" [ Expression ]"}}"        ;

separator FieldBinding ","										  ;
ObjectConstr. Constructor      ::= "{" [ FieldBinding ] "}"       ;
FieldBinding. FieldBinding ::= Expression ":" Expression		  ;


{-  . SelectStatement                                            -}
-- TODO find more effective way to do maybes
SelectStmt. SelectStatement ::=
	MWithClause
	SelSetOper
	MOrder
	MLimitClause                                                  ;

separator UnionedSets ""                                            ;
SelSetOper. SelSetOper ::= SelectBlock [UnionedSets]                ;
SelBlockUnionedSets. UnionedSets ::= "union" "all" SelectBlock      ;
SubqueryUnionedSets. UnionedSets ::= "union" "all" SelectStatement1 ;

_. SelectStatement1           ::= "(" SelectStatement ")"         ;

FirstSelBlock. SelectBlock ::= SelClause MFromDefVal MWhere MGroup MOrder MDefVal MHaving ;
SeconSelBlock. SelectBlock ::= FromClause MDefVal MWhere MGroup MOrder MDefVal MHaving SelClause ;

IsMFromDefVal. MFromDefVal ::= FromClause MDefVal ;
NoMFromDefVal. MFromDefVal ::=                          ;

IsMWhere. MWhere ::= WhereClause ;
NoMWhere. MWhere ::=             ;

IsMGroupDefValHaving. MGroupDefValHaving ::= GroupbyClause MDefVal MHaving ;
NoMGroupDefValHaving. MGroupDefValHaving ::=                               ;

LetMDefVal. MDefVal ::= LetClause  ;
WitMDefVal. MDefVal ::= WithClause ;
NonMDefVal. MDefVal ::=            ;

SelClause. SelClause ::= "select" MTypeSelClause SelRegOrVal ;

RegSelRegOrVal. SelRegOrVal ::= SelReg ;
ValSelRegOrVal. SelRegOrVal ::= SelVal ;

AllMTypeSelClause. MTypeSelClause ::= "all"      ;
DisMTypeSelClause. MTypeSelClause ::= "distinct" ;
NonMTypeSelClause. MTypeSelClause ::=            ;

separator nonempty Projection "," ;
SelReg. SelReg ::= [Projection]   ;

ValueSelValType. SelValType ::= "value"		;
ElemtSelValType. SelValType ::= "element"	;
Raw__SelValType. SelValType ::= "raw"		;

SelVal. SelVal        ::= SelValType Expression				;
AllProjection. Projection ::= "*"							;
ExpProjection. Projection ::= Expression MAsIdentifier ;

NoMAsIdent. MAsIdentifier ::=				 ;
IsMAsIdent. MAsIdentifier ::= MAs Identifier ;

NoMAs. MAs ::=      ;
IsMAs. MAs ::= "as" ;

separator nonempty FromTerm "," ;
FromClause. FromClause ::= "from" [FromTerm] ;

separator FromTermJoin "" ;
FromTerm. FromTerm         ::= ExprMVarMAt [FromTermJoin] ;
FromTermJoin. FromTermJoin ::= MJoinType JoinOrUnnest  ;
JoinJoinOrUnnest. JoinOrUnnest ::= "join" ExprMVarMAt "on" Expression ;
UnnrJoinOrUnnest. JoinOrUnnest ::= UnnestClauseType Expression MAs Variable MAtVar ;

ExprMVar. ExprMVarMAt ::= Expression MMAsVar MAtVar ;
NoMMAsVar. MMAsVar ::=              ;
IsMMAsVar. MMAsVar ::= MAs Variable ;

UnnestUnnestClauseType. UnnestClauseType ::= "unnest"    ;
CorrelUnnestClauseType. UnnestClauseType ::= "correlate" ;
FlatteUnnestClauseType. UnnestClauseType ::= "flatten"   ;

IsMAtVar. MAtVar ::= "at" Variable ;
NoMAtVar. MAtVar ::=               ;

IsMJoinType. MJoinType ::= JoinType ;
NoMJoinType. MJoinType ::=          ;

InnerJoinType. JoinType ::= "inner"        ;
LeftNJoinType. JoinType ::= "left"         ;
LeftIJoinType. JoinType ::= "left" "outer" ;

separator nonempty WithElement "," ;
WithClause. WithClause ::= "with" [WithElement] ;

IsMWithClause. MWithClause ::= WithClause ;
NoMWithClause. MWithClause ::=            ;

separator nonempty LetElement "," ;
LetClause. LetClause ::= LetLetClause [LetElement]	;
FirstLetLetClause. LetLetClause ::= "let"			;
SeconLetLetClause. LetLetClause ::= "letting"		;

LetElement.  LetElement  ::= Variable "=" Expression	 ;
WithElement. WithElement ::= Variable "as" Expression  ;

WhereClause. WhereClause ::= "where" Expression ;

separator nonempty ExprMVarMAt "," ;
GroupbyClause. GroupbyClause ::= "group" "by" [ExprMVarMAt] MaybeGroupAs ;
IsMGroup. MGroup ::= GroupbyClause ;
NoMGroup. MGroup ::=               ;

IsMaybeGroupAs. MaybeGroupAs ::= "group" "as" Variable MaybeVarAsRef ;
NoMaybeGroupAs. MaybeGroupAs ::= ;

separator nonempty VarAsRef "," ;
IsMaybeVarAsRef. MaybeVarAsRef ::= "(" [VarAsRef] ")"  ;
VarAsRef. VarAsRef ::= Variable "as" VariableRef ;

NoMaybeVarAsRef. MaybeVarAsRef ::=                   ;

HavingClause. HavingClause ::= "having" Expression ;
IsMHaving. MHaving ::= HavingClause ;
NoMHaving. MHaving ::=				;


IsMOrder. MOrder ::= OrderBy ;
NoMOrder. MOrder ::=               ;

separator nonempty ExprOrderbyClause "," ;
OrderBy. OrderBy ::= "order" "by" [ExprOrderbyClause] ;
ExprOrderByClause. ExprOrderbyClause ::= Expression MaybeOrdOrderbyClause ;

AscOrdClause. MaybeOrdOrderbyClause ::= "asc"  ;
DesOrdClause. MaybeOrdOrderbyClause ::= "desc" ;
NonOrdClause. MaybeOrdOrderbyClause ::=        ;

IsMLimitClause. MLimitClause ::= LimitClause ;
NoMLimitClause. MLimitClause ::=			 ;

LimitClause. LimitClause ::= "limit" Expression MOffsetExpr ;

IsMOffsetExpr. MOffsetExpr ::= "offset" Expression ;
NoMOffsetExpr. MOffsetExpr ::=                     ;

{-                     VariableRef                               -}
VariableRef. VariableRef ::= Identifier ;
QualifieRef. VariableRef ::= DelimIdent ;

Variable. Variable ::= Identifier ;   -- check whether thats ok

comment "//" ;
comment "#"  ;
comment "--" ;
comment "/*" "*/" ;